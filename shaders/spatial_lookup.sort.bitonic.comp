#version 450

layout (local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

#define GRID_WRITEABLE
#define GRID_PCR
#include "spatial_lookup.glsl"

void main() {
	uint n = constants.sort_n;
	uint k = constants.sort_k;
	uint j = constants.sort_j;

	uint group_number = gl_GlobalInvocationID.x / j;
	uint group_index = gl_GlobalInvocationID.x % j;

	uint i = 2 * group_number * j + group_index;
	uint l = i ^ j;

	SpatialCacheEntry cache_i = spatial_cache[i];
	uint64_t key_i = (uint64_t(cache_i.cellKey) << 32) + cache_i.cellClass;

	SpatialCacheEntry cache_l = spatial_cache[l];
	uint64_t key_l = (uint64_t(cache_l.cellKey) << 32) + cache_l.cellClass;

	if (!(((i & k) == 0 && key_i > key_l) || ((i & k) != 0 && key_i < key_l))) return;

	SpatialLookupEntry value_i = spatial_lookup[i];
	SpatialLookupEntry value_l = spatial_lookup[l];

	spatial_cache[i] = cache_l;
	spatial_lookup[i] = value_l;

	spatial_cache[l] = cache_i;
	spatial_lookup[l] = value_i;

}
