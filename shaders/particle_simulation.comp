#version 450

layout(local_size_x_id = 0, local_size_y = 1, local_size_z = 1) in;

layout(binding = 0) buffer positionBuffer { vec2 positions[]; };
layout(binding = 1) buffer velocityBuffer { vec2 velocities[]; };
layout(binding = 2) buffer densityBuffer { float densities[]; };
layout(binding = 5) buffer velocityOutputBuffer { vec2 velocitiesOutput[]; };

layout(push_constant) uniform PushStruct {
    float gravity;
    float deltaTime;
    uint numParticles;
    float collisionDampingFactor;
    float spatialRadius;
    float targetDensity;
    float pressureMultiplier;
    float viscosity;
}
constants;

#define GRID_NUM_ELEMENTS constants.numParticles
#define GRID_CELL_SIZE constants.spatialRadius
#define GRID_BINDING_LOOKUP 3
#define GRID_BINDING_INDEX 4
#define COORDINATES_BUFFER_NAME positions
#include "spatial_lookup.glsl"

const float PI = 3.14159265359;
const float particleMass = 1.0;

float viscosityKernel(float radius, float dist) {
    float volume = PI * pow(radius, 8) / 4;
    float value = max(0, radius * radius - dist * dist);
    return value * value * value / volume;
}

float smoothingKernel(float radius, float dist) {
    if (dist >= radius) return 0.0;
    float volume = (PI * pow(radius, 4)) / 6.0;
    return (radius - dist) * (radius - dist) / volume;
}

float smoothingKernelDerivative(float radius, float dist) {
    if (dist >= radius) return 0.0;

    float scale = 12.0 / (PI * pow(radius, 4));
    return (dist - radius) * scale;
}

float density2pressure(float density) {
    return constants.pressureMultiplier * (density - constants.targetDensity);
}

float calculateSharedPressure(float density, float neighbourDensity) {
    return (density2pressure(density) + density2pressure(neighbourDensity)) / 2;
}

void addPressureForce(inout vec2 pressureForce, const vec2 pos, const float density, const float radius, const float mass, uint neighbourIndex, vec2 neighbourPosition, float neighbourDistance) {
    vec2 diff = pos - neighbourPosition;
    float dist = length(diff);
    if (dist >= radius) return;
    vec2 direction;
    if (dist == 0.0) {
        return;
    } else {
        direction = diff / dist;
    }
    float slope = -smoothingKernelDerivative(radius, dist);
    float sharedPressure = calculateSharedPressure(density, densities[neighbourIndex]);
    pressureForce += sharedPressure * direction * slope * mass / density;
}

void addViscosityForce(inout vec2 viscosityForce, const vec2 pos, const vec2 velocity, const float radius, uint neighbourIndex, vec2 neighbourPosition, float neighbourDistance) {
    vec2 diff = pos - neighbourPosition;
    float dist = length(diff);
    float influence = viscosityKernel(radius, dist);
    viscosityForce += (velocities[neighbourIndex] - velocity) * influence;
}

vec2 calculatePressureForce(vec2 pos, const float density, float radius) {
    vec2 pressureForce = vec2(0.0, 0.0);
#ifdef DEF_2D
    FOREACH_NEIGHBOUR(pos, {
        addPressureForce(pressureForce, pos, density, radius, particleMass, NEIGHBOUR_INDEX, NEIGHBOUR_POSITION, NEIGHBOUR_DISTANCE);
    });
#endif
    return pressureForce;
}

vec2 calculateViscosityForce(vec2 position, vec2 velocity, float radius) {
    vec2 viscosityForce = vec2(0.0, 0.0);
#ifdef DEF_2D
    FOREACH_NEIGHBOUR(position, {
        addViscosityForce(viscosityForce, position, velocity, constants.spatialRadius, NEIGHBOUR_INDEX, NEIGHBOUR_POSITION, NEIGHBOUR_DISTANCE);
    });
#endif
    return viscosityForce * constants.viscosity;
}

void main() {
    uint index = gl_GlobalInvocationID.x;
    if (index >= constants.numParticles) return;

    vec2 position = positions[index];
    vec2 velocity = velocities[index];
    float density = densities[index];

    vec2 pressureForce = calculatePressureForce(position, density, constants.spatialRadius);
    vec2 pressureAcceleration = pressureForce / density;

    vec2 viscosityForce = calculateViscosityForce(position, velocity, constants.spatialRadius);

    // Update velocity
    velocity += pressureAcceleration * constants.deltaTime;
    velocity += viscosityForce * constants.deltaTime;
    velocity += vec2(0.0, constants.gravity * constants.deltaTime);

    velocitiesOutput[index] = velocity;
}